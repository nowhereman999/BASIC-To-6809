0 ' SPIRALS - BY TAZMAN (SCOTT COOPER)
1 DIM X(128),Y(128)
2 GMODE 16,1:GCLS 0:SCREEN1,1:B=96
3 FOR S=1 TO 40:A=128:H=1
4 FOR R=S TO 160 STEP S:H=1-H
5 GOSUB 6:NEXT:GCLS 0:NEXT:GOTO 3
6 X=0:Y=R:D=3-2*R:A=256+S-A:J=0:X(0)=X:Y(0)=Y
7 Y1=B:IF H THEN X1=A+Y:GOSUB 18 ELSE X1=A-Y:GOSUB 18
8 IF D<=0 THEN D=D+4*X+6 ELSE D=D+4*(X-Y)+10:Y=Y-1
9 X=X+1:J=J+1:X(J)=X:Y(J)=Y:IF H THEN X1=A+Y:Y1=B+X ELSE X1=A-Y:Y1=B-X
10 GOSUB 18:IF X<Y THEN 8
11 IF H THEN 15
12 FOR I=J TO 0 STEP -1:X1=A-X(I):Y1=B-Y(I):GOSUB 18:NEXT
13 FOR I=0 TO J:X1=A+X(I):Y1=B-Y(I):GOSUB 18:NEXT
14 FOR I=J TO 0 STEP -1:X1=A+Y(I):Y1=B-X(I):GOSUB 18:NEXT:RETURN
15 FOR I=J TO 0 STEP -1:X1=A+X(I):Y1=B+Y(I):GOSUB 18:NEXT
16 FOR I=0 TO J:X1=A-X(I):Y1=B+Y(I):GOSUB 18:NEXT
17 FOR I=J TO 0 STEP -1:X1=A-Y(I):Y1=B+X(I):GOSUB 18:NEXT:RETURN
18 IF X1<0 OR X1>255 THEN RETURN
19 IF Y1<0 OR Y1>191 THEN RETURN
20 SET(X1,Y1,1):RETURN
